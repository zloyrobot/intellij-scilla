plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.6.10'
    id "org.jetbrains.intellij" version "1.3.0"
    id "org.jetbrains.grammarkit" version "2021.1.3"
}


sourceCompatibility = 1.8

compileKotlin.kotlinOptions {
	jvmTarget = 1.8
	freeCompilerArgs = ['-Xjvm-default=compatibility']
}


idea.module.generatedSourceDirs += file('gen')

sourceSets.main.java.srcDirs += 'gen'

intellij {
    pluginName = 'Scilla'
    version = '2021.3.1'
    downloadSources = true
}

repositories {
    mavenCentral()
    maven { url "https://www.jetbrains.com/intellij-repository/releases" }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.6.10"
    testImplementation 'junit:junit:4.13.2'
}


task lexer(type: org.jetbrains.grammarkit.tasks.GenerateLexer) {
    source = "$project.projectDir/src/main/kotlin/com/zloyrobot/scilla/lang/_ScillaLexer.flex"
    targetDir = "gen/com/zloyrobot/scilla/lexer"
    targetClass = "ScillaLexerGenerated"
}

tasks.compileKotlin.dependsOn lexer
